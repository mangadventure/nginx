# Maintainer: ObserverOfTime <chronobserver@disroot.org>

# shellcheck disable=SC2154,SC2016,SC2206,SC2046

pkgname=freenginx-custom
pkgver=1.29.4
pkgrel=1
pkgdesc='Lightweight HTTP and reverse proxy server (custom build)'
url='https://freenginx.org'
license=('BSD-2-Clause')
arch=(x86_64)
depends=(liburing libxcrypt mimalloc pcre2 zlib)
makedepends=(git zstd)
optdepends=(
  'brotli: brotli compression module'
  'memcached: memcached protocol module'
  'redis: redis protocol module'
)
conflicts=(nginx freenginx-mainline)
provides=(nginx "freenginx-mainline=${pkgver}")
backup=(
  etc/nginx/nginx.conf
  etc/nginx/mime.types
  etc/nginx/uwsgi_params
  etc/nginx/default.vhost
  etc/logrotate.d/nginx
)
source=(
  "https://freenginx.org/download/freenginx-${pkgver}.tar.gz"{,.asc}
  'git+https://github.com/defo-project/openssl.git#branch=3.6.0-ech'
  'ngx_brotli::git+https://github.com/google/ngx_brotli.git'
  'ngx_ct::git+https://github.com/grahamedgecombe/nginx-ct.git'
  'ngx_memc::git+https://github.com/openresty/memc-nginx-module.git'
  'ngx_redis2::git+https://github.com/openresty/redis2-nginx-module.git'
  'ngx_vts::git+https://github.com/vozlt/nginx-module-vts.git'
  'ngx_zstd::git+https://github.com/tokers/zstd-nginx-module.git'
  '001-limit-req.patch' # https://mailman.nginx.org/pipermail/nginx-devel/2018-February/010856.html
  '002-io-uring.patch' # https://github.com/centminmod/centminmod/blob/master/patches/nginx/nginx_io_uring.patch
  '003-dynamic-tls.patch' # https://github.com/karljohns0n/nginx-more/blob/master/SOURCES/ngx_dynamic-tls-records-1.29.2.patch
  'dhparam.pem::https://ssl-config.mozilla.org/ffdhe4096.txt'
  'default.vhost'
  'logrotate'
  'mime.types'
  'nginx.conf'
  'nginx.service'
)
validpgpkeys=(
  'B0F4253373F8F6F510D42178520A9993A1C052F8' # Maxim Dounin <mdounin@mdounin.ru>
)
sha256sums=('51a596451e334b51ce8cef1291b576ed601ed557e1b500e6c1a77a469d603e27'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
            'd6f81c50e4e850c9ae6886b2d1d44e90cb04d4e3c75f16509dad36f5f43edb73'
            'bd4390a2d192b4ef90b27163b742751a59d973d70337d1264215aa462bed0a56'
            'b8e41f81fc38542bcc8b6225a56d2fd534b56a983650436477ec03582f99c1c3'
            '64852d6890ff9e62eecd1ee89c72af9af244dfef5b853bcedea3dfd7aade22b3'
            'd4a7eaf8a3d5c89c316cc243da69588afd6176b4b2b9e85f9404be3bb245706f'
            'f891a825436e798de935c67bae35e9a80ee7f8430ca6667dd525a7cacff6e606'
            '77ea37efe02510c014f9379c926e889efbf93422e7b474cfea41f8545d13f581'
            '13cd56043431ef5a4a10fba5b501f5df6bd9c0c4b3f6ac8c594260d88528270e'
            'adb4a2b5176be3a3bf39666584f7a0a7f10b1b1aca927c189c1910c789d6d13c')

prepare() {
  msg2 'Updating brotli submodule'
  git -C ngx_brotli submodule update --init --depth=1

  cd "freenginx-${pkgver}"

  msg2 'Stripping HTTP server tokens'
  sed -i src/core/nginx.h -e 's/"freenginx"/"ws"/;s|NGINX_NAME "/" NGINX_VERSION|NGINX_NAME|'

  msg2 'Applying "Limit req: r/h and r/d support" patch'
  patch -Nbp1 < ../001-limit-req.patch
  msg2 'Applying "Add io_uring support" patch'
  patch -Nbp1 < ../002-io-uring.patch
  msg2 'Applying "Add TLS Dynamic Record Resizing" patch'
  patch -Nbp1 < ../003-dynamic-tls.patch
}

build() {
  cd "freenginx-${pkgver}"

  ./configure \
    --prefix=/etc/nginx \
    --sbin-path=/usr/bin/nginx \
    --modules-path=/var/lib/nginx/modules \
    --conf-path=/etc/nginx/nginx.conf \
    --error-log-path=/var/log/nginx/error.log \
    --http-log-path=/var/log/nginx/access.log \
    --pid-path=/run/nginx.pid \
    --lock-path=/run/lock/nginx.lock \
    --http-client-body-temp-path=/var/cache/nginx/client_temp \
    --http-proxy-temp-path=/var/cache/nginx/proxy_temp \
    --http-uwsgi-temp-path=/var/cache/nginx/uwsgi_temp \
    --user=nginx \
    --group=nginx \
    --with-compat \
    --with-file-aio \
    --with-threads \
    --with-pcre \
    --with-pcre-jit \
    --with-mail \
    --with-mail_ssl_module \
    --without-mail_pop3_module \
    --with-http_auth_request_module \
    --with-http_gunzip_module \
    --with-http_gzip_static_module \
    --with-http_realip_module \
    --with-http_slice_module \
    --with-http_ssl_module \
    --with-http_v2_module \
    --with-http_v3_module \
    --without-http_browser_module \
    --without-http_empty_gif_module \
    --without-http_fastcgi_module \
    --without-http_geo_module \
    --without-http_memcached_module \
    --without-http_mirror_module \
    --without-http_scgi_module \
    --without-http_split_clients_module \
    --without-http_userid_module \
    --with-openssl=../openssl \
    --with-cc-opt="$CFLAGS $CPPFLAGS" \
    --with-ld-opt="$LDFLAGS -lmimalloc" \
    --add-dynamic-module=../ngx_brotli \
    --add-dynamic-module=../ngx_ct \
    --add-dynamic-module=../ngx_memc \
    --add-dynamic-module=../ngx_redis2 \
    --add-dynamic-module=../ngx_vts \
    --add-dynamic-module=../ngx_zstd
  make -j$(getconf _NPROCESSORS_ONLN)
}


package() {
  cd freenginx-$pkgver
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/etc/nginx/*.default \
     "$pkgdir"/etc/nginx/koi-win \
     "$pkgdir"/etc/nginx/koi-utf \
     "$pkgdir"/etc/nginx/win-utf \
     "$pkgdir"/etc/nginx/scgi_params \
     "$pkgdir"/etc/nginx/fastcgi_params \
     "$pkgdir"/etc/nginx/fastcgi.conf
  rm -r "$pkgdir"/run "$pkgdir"/etc/nginx/html

  chmod 755 "$pkgdir"/var/log/nginx
  chown root:root "$pkgdir"/var/log/nginx

  install -d "$pkgdir"/var/lib/nginx
  install -d "$pkgdir"/var/cache/nginx
  install -d "$pkgdir"/etc/nginx/sites-available
  install -d "$pkgdir"/etc/nginx/sites-enabled

  install -Dm644 ../nginx.conf "$pkgdir"/etc/nginx
  install -Dm644 ../mime.types "$pkgdir"/etc/nginx
  install -Dm644 ../default.vhost "$pkgdir"/etc/nginx

  install -Dm644 ../dhparam.pem "$pkgdir"/etc/ssl/dhparam.pem
  install -Dm644 ../logrotate "$pkgdir"/etc/logrotate.d/nginx
  install -Dm644 ../nginx.service "$pkgdir"/usr/lib/systemd/system/nginx.service

  install -Dm644 objs/nginx.8 "$pkgdir"/usr/share/man/man8/nginx.8
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE

  for f in objs/ngx_*_module.so; do
    install -Dm644 "$f" "$pkgdir"/var/lib/nginx/modules
  done

  for d in ftdetect ftplugin indent syntax; do
    install -Dm644 contrib/vim/$d/nginx.vim \
      "$pkgdir/usr/share/vim/vimfiles/$d/nginx.vim"
  done

  sed -i "$pkgdir"/etc/nginx/nginx.conf \
      -e '/^user/s|nginx|http|;/^pid/s|/var||'

  printf >> "$pkgdir"/etc/nginx/uwsgi_params \
    '\nuwsgi_param HTTP_EARLY_DATA $ssl_early_data if_not_empty;\n'
}
